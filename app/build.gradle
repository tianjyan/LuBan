import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'checkstyle'
apply plugin: 'me.tatarka.retrolambda'

def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    Calendar date = new GregorianCalendar();
    date.set(Calendar.HOUR_OF_DAY, 0);
    date.set(Calendar.MINUTE, 0);
    date.set(Calendar.SECOND, 0);
    date.set(Calendar.MILLISECOND, 0);
    return df.format(date.getTime())
}

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "org.tianjyan.luban"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        buildConfigField "boolean", "ENABLE_LEAK_CANARY", "false"
        buildConfigField "String", "GIT_COMMIT_SHA", '"' + gitSha() + '"'
        buildConfigField "String", "BUILD_TIME", '"' + buildTime() + '"'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }
    buildTypes {
        debugMemoryLeak {
            initWith debug
            buildConfigField "boolean", "ENABLE_LEAK_CANARY", "true"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile fileTree(include: ['*.aidl'], dir: '../aidl')
    compile project(':luban.aidl')
    compile 'com.jakewharton:butterknife:8.7.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.facebook.stetho:stetho:1.5.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.7.0'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    debugMemoryLeakCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
}

task styleCheck(type: Checkstyle) {
    configFile new File(rootDir, "config/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**', '**/mobeta/**'

    classpath = files()
}